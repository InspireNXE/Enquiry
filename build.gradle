plugins {
    id 'java'
    id 'com.github.hierynomus.license' version '0.11.0'
    id 'com.github.johnrengelman.shadow' version '1.2.1'
}

defaultTasks 'clean', 'build'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

archivesBaseName = 'enquiry'
version = '1.2'
ext.name = 'Enquiry'
ext.organization = 'InspireNXE'
ext.url = 'http://github.com/InspireNXE/'

repositories {
    mavenCentral()
    maven {
        name 'Sponge maven repo'
        url 'http://repo.spongepowered.org/maven'
    }
}

dependencies {
    compile 'com.github.kevinsawicki:http-request:6.0'
    compile 'org.spongepowered:spongeapi:2.1-SNAPSHOT'
}

shadowJar {
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    dependencies {
        include(dependency("com.github.kevinsawicki:http-request:6.0"))
    }
    classifier = ''
}

license {
    ext.name = project.name
    ext.organization = organization
    ext.url = url
    header rootProject.file('HEADER.txt')
    ignoreFailures true
    strictCheck true
    include '**/*.java'
}

build.dependsOn shadowJar
build.dependsOn licenseFormatMain

if (JavaVersion.current().isJava8Compatible() || JavaVersion.current().isJava9Compatible()) {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}